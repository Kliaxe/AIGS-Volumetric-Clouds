
// This shader expects volumetricclouds-utils.glsl to be concatenated before it at load time

layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;
layout(rgba32f, binding = 0) uniform image3D ShapeOut;

uniform ivec3 VolumeSize; // xyz size

void main()
{
    ivec3 tid = ivec3(gl_GlobalInvocationID.xyz);
    if (any(greaterThanEqual(tid, VolumeSize))) return;

    vec3 pos = (vec3(tid) + 0.5) / vec3(VolumeSize);

    const float perlinWorleyDifference = 0.3;
    const float perlinDilateLowRemap = 0.3;
    const float perlinDilateHighRemap = 1.4;
    const float worleyDilateLowRemap = -0.3;
    const float worleyDilateHighRemap = 1.3;
    const float totalWorleyLowRemap = -0.4;
    const float totalWorleyHighRemap = 1.0;
    const float totalSize = 1.0;

    float perlinDilateNoise = GetPerlin_7_Octaves(pos, 4.0 * totalSize, true);
    float worleyDilateNoise = GetWorley_3_Octaves(pos, 6.0 * totalSize);

    float worleyLarge = GetWorley_3_Octaves(pos, 6.0 * totalSize);
    float worleyMedium = GetWorley_3_Octaves(pos, 12.0 * totalSize);
    float worleySmall = GetWorley_3_Octaves(pos, 24.0 * totalSize);

    perlinDilateNoise = Remap01(perlinDilateNoise, perlinDilateLowRemap, perlinDilateHighRemap);
    worleyDilateNoise = Remap01(worleyDilateNoise, worleyDilateLowRemap, worleyDilateHighRemap);

    worleyLarge = Remap01(worleyLarge, totalWorleyLowRemap, totalWorleyHighRemap);
    worleyMedium = Remap01(worleyMedium, totalWorleyLowRemap, totalWorleyHighRemap);
    worleySmall = Remap01(worleySmall, totalWorleyLowRemap, totalWorleyHighRemap);

    float perlinWorleyNoise = DilatePerlinWorley(perlinDilateNoise, worleyDilateNoise, perlinWorleyDifference);

    vec4 c = vec4(perlinWorleyNoise, worleyLarge, worleyMedium, worleySmall);
    imageStore(ShapeOut, tid, c);
}
